// SPDX-License-Identifier: BUSL-1.1

pragma solidity ^0.8.4;

import "./RedstoneConstants.sol";
import "./CalldataExtractor.sol";

/**
 * @title The base contract for forwarding redstone payload to other contracts
 * @author The Redstone Oracles team
 */
contract ProxyConnector is RedstoneConstants, CalldataExtractor {
  error ProxyCalldataFailedWithoutErrMsg();
  error ProxyCalldataFailedWithStringMessage(string message);
  error ProxyCalldataFailedWithCustomError(bytes result);

  function proxyCalldata(
    address contractAddress,
    bytes memory encodedFunction,
    bool forwardValue
  ) internal returns (bytes memory) {
    bytes memory message = _prepareMessage(encodedFunction);

    (bool success, bytes memory result) =
      contractAddress.call{value: forwardValue ? msg.value : 0}(message);

    return _prepareReturnValue(success, result);
  }

  function proxyDelegateCalldata(address contractAddress, bytes memory encodedFunction)
    internal
    returns (bytes memory)
  {
    bytes memory message = _prepareMessage(encodedFunction);
    (bool success, bytes memory result) = contractAddress.delegatecall(message);
    return _prepareReturnValue(success, result);
  }

  function proxyCalldataView(address contractAddress, bytes memory encodedFunction)
    internal
    view
    returns (bytes memory)
  {
    bytes memory message = _prepareMessage(encodedFunction);
    (bool success, bytes memory result) = contractAddress.staticcall(message);
    return _prepareReturnValue(success, result);
  }

  function _prepareMessage(bytes memory encodedFunction) private pure returns (bytes memory) {
    uint256 encodedFunctionBytesCount = encodedFunction.length;
    uint256 redstonePayloadByteSize = _getRedstonePayloadByteSize();
    uint256 resultMessageByteSize = encodedFunctionBytesCount + redstonePayloadByteSize;

    if (redstonePayloadByteSize > msg.data.length) {
      revert CalldataOverOrUnderFlow();
    }

    bytes memory message;

    assembly {
      message := mload(FREE_MEMORY_PTR) // sets message pointer to first free place in memory

      // Saving the byte size of the result message (it's a standard in EVM)
      mstore(message, resultMessageByteSize)

      // Copying function and its arguments
      for {
        let from := add(BYTES_ARR_LEN_VAR_BS, encodedFunction)
        let fromEnd := add(from, encodedFunctionBytesCount)
        let to := add(BYTES_ARR_LEN_VAR_BS, message)
      } lt (from, fromEnd) {
        from := add(from, STANDARD_SLOT_BS)
        to := add(to, STANDARD_SLOT_BS)
      } {
        // Copying data from encodedFunction to message (32 bytes at a time)
        mstore(to, mload(from))
      }

      // Copying redstone payload to the message bytes
      calldatacopy(
        add(message, add(BYTES_ARR_LEN_VAR_BS, encodedFunctionBytesCount)), // address
        sub(calldatasize(), redstonePayloadByteSize), // offset
        redstonePayloadByteSize // bytes length to copy
      )

      // Updating free memory pointer
      mstore(
        FREE_MEMORY_PTR,
        add(
          add(message, add(redstonePayloadByteSize, encodedFunctionBytesCount)),
          BYTES_ARR_LEN_VAR_BS
        )
      )
    }

    return message;
  }

  function _getRedstonePayloadByteSize() private pure returns (uint256) {
    uint256 calldataNegativeOffset = _extractByteSizeOfUnsignedMetadata();
    uint256 dataPackagesCount;
    (dataPackagesCount, calldataNegativeOffset) = _extractDataPackagesCountFromCalldata(calldataNegativeOffset);
    for (uint256 dataPackageIndex = 0; dataPackageIndex < dataPackagesCount; dataPackageIndex++) {
      uint256 dataPackageByteSize = _getDataPackageByteSize(calldataNegativeOffset);
      calldataNegativeOffset += dataPackageByteSize;
    }

    return calldataNegativeOffset;
  }

  function _prepareReturnValue(bool success, bytes memory result)
    internal
    pure
    returns (bytes memory)
  {
    if (!success) {

      if (result.length == 0) {
        revert ProxyCalldataFailedWithoutErrMsg();
      } else {
        bool isStringErrorMessage;
        assembly {
          let first32BytesOfResult := mload(add(result, BYTES_ARR_LEN_VAR_BS))
          isStringErrorMessage := eq(first32BytesOfResult, STRING_ERR_MESSAGE_MASK)
        }

        if (isStringErrorMessage) {
          string memory receivedErrMsg;
          assembly {
            receivedErrMsg := add(result, REVERT_MSG_OFFSET)
          }
          revert ProxyCalldataFailedWithStringMessage(receivedErrMsg);
        } else {
          revert ProxyCalldataFailedWithCustomError(result);
        }
      }
    }

    return result;
  }
}
